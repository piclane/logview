group 'me.piclane'
version '2.0.0'

apply plugin: 'java'
apply plugin: 'war'

sourceCompatibility = 1.8
targetCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    def jerseyVersion = 2.25
    compileOnly 'javax.websocket:javax.websocket-api:1.1'
    compileOnly 'javax.servlet:javax.servlet-api:3.1.0'

    implementation 'javax.servlet:jstl:1.2'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.github.albfernandez:juniversalchardet:2.2.0'
    implementation 'jchardet:jchardet:1.1.0'
    implementation "org.glassfish.jersey.containers:jersey-container-servlet:${jerseyVersion}"
    implementation "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"
    implementation "org.glassfish.jersey.core:jersey-server:${jerseyVersion}"
    implementation "org.glassfish.jersey.media:jersey-media-json-jackson:${jerseyVersion}"
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// ブランチ名を取得する
static def readBranch() {
    return "git name-rev --name-only HEAD"
            .execute().text
            .trim()
            .replaceAll(/remotes\/origin\//, "")
            .replaceAll(/\^0$/, "")
}

war {
    exclude '**/WEB-INF/web.xml'
    webXml = file("${buildDir}/web/WEB-INF/web.xml")
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    def branchName = readBranch()
    switch (branchName) {
        case { System.getProperty("idea.active", "false") == "true" }:
            archiveName = "${project.name}.war"
            break
        case 'master':
        case 'develop':
        case ~/^tags\/.+$/:
        case { project.version ==~ /^.*_.+$/ }:
            archiveName = "${project.name}_${project.version}.war"
            break
        default:
            branchName = branchName.tr('\\/:*?"<>|', '＼／：＊？”＜＞｜')
            archiveName = "${project.name}_${project.version}_${branchName}.war"
            break
    }
}
